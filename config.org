* Configuring =org-publish-project-alist=
:PROPERTIES:
:ID:       225F5809-E04A-495C-ACDA-3B7BCBF996D9
:PUBDATE:  <2019-03-18 Mon 16:12>
:END:

Publication happens using @@html:<kbd>M-x</kbd> <kbd>org-publish</kbd>@@ which
uses the value of [[https://orgmode.org/manual/Project-alist.html][=org-publish-project-alist=]]. We pass the values to the
property list and it configures the blog.

These are the elements of the configuration:

#+TOC: headlines 1 local

** Bootstrap and [[https://github.com/mnater/Hyphenator][Hyphenator]] 
:PROPERTIES:
:ID:       75A0CF06-1618-4D80-B355-B32E3FFD5DF1
:END:

The property =:html-head= adds text to the =<head>= tag of the document.

#+begin_src emacs-lisp
(setq blog-html-head-extra
      "<link rel=\"stylesheet\" href=\"/assets/style.css\" type=\"text/css\">
       <script src=\"/assets/hyphenator.js\" type=\"text/javascript\"></script>
       <script src=\"/assets/hylo.js\" type=\"text/javascript\"></script>
       <link rel=\"stylesheet\" href=\"https://stackpath.bootstrapcdn.com/bootstrap/4.3.1/css/bootstrap.min.css\">")
#+end_src

This adds Bootstrap and HyphenatorJS along with my [[file:src/assets/style.css][custom CSS]].

** Navigation
:PROPERTIES:
:ID:       8734C1A6-C55D-4792-BF87-1B99D3BF229D
:END:

The navigation bar that is shared by all pages is in the =:html-home/up-format=
property. This is a format string with two arguments: the root directory and the
directory above the current directory.

#+begin_src emacs-lisp
(setq blog-html-up
      "<h1 class=\"my-3 mt-sm-5 h3 text-center\">Antoine Kalmbach</h1>
       <ul class=\"nav justify-content-center mb-5\">
         <li class=\"nav-item\">
          <a class=\"nav-link\" href=\"%s\">Index</a>
         </li>
         <li class=\"nav-item\">
          <a class=\"nav-link\" href=\"/about.html\">About</a>
         </li>
         <li class=\"nav-item\">
          <a class=\"nav-link\" href=\"/archive.html\">Archive</a>
         </li>
         <li class=\"nav-item\">
          <a class=\"nav-link\" href=\"/feed.xml\">RSS</a>
         </li>
       </ul>")
#+end_src

#+RESULTS:
#+begin_example
<h1 class="my-3 mt-sm-5 h3 text-center">Antoine Kalmbach</h1>
       <ul class="nav justify-content-center mb-5">
         <li class="nav-item">
          <a class="nav-link" href="%s">Index</a>
         </li>
         <li class="nav-item">
          <a class="nav-link" href="/about.html">About</a>
         </li>
         <li class="nav-item">
          <a class="nav-link" href="/archive.html">Archive</a>
         </li>
         <li class="nav-item">
          <a class="nav-link" href="/feed.xml">RSS</a>
         </li>
       </ul>
#+end_example

** Titles
:PROPERTIES:
:ID:       B8EF6B4B-0B32-4CFB-8F50-F89B7B5BC593
:END:

Page and post titles are rendered using the /preamble/ defined in
=:html-preamble=. This is a function that accepts a property list of the current
project. I extract the title and date and render the page using standard
Bootstrap styles.

#+begin_src emacs-lisp
  (defun blog-html-preamble-fmt (plist)
    (when (plist-get plist :title)
      (let*
          ((dir (plist-get plist :publishing-directory))
           (path (file-relative-name (plist-get plist :output-file) dir)))
      
        (format
         "<h2 class=\"page-header\"> <a href=\"/%s\">%s</a> </h2>
          <p class=\"text-muted post-meta\">%s</p>"
         path
         (car (plist-get plist :title))
         (org-timestamp-format (car (plist-get plist :date)) "%d %B %Y")))))
#+end_src


** Footer
:PROPERTIES:
:ID:       30323E30-3135-43FD-8B12-ECAB417B7D35
:END:

The footer renders using the /postamble/ defined in =:html-postamble=.

#+begin_src emacs-lisp
(setq blog-html-down
      "<hr><address>Last modified on %T. Content licensed under <a href=\"https://creativecommons.org/licenses/by-nc-sa/4.0/\">CC BY-NC-SA 4.0</a>.")
#+end_src

** Publishing functions
:PROPERTIES:
:ID:       77146A29-61C8-43F7-8403-99754BA462F2
:END:

Here, at last, we have to resort to some glorious Lisp code to get the results I
want: the body rendered inside a Bootstrap =.container= class. I found this from
[[http://vicarie.in][Narendra Joshi]]'s blog:

#+begin_src emacs-lisp
  (defun blog-publish-html (plist filename pub-dir)
    "Same as `org-html-publish-to-html' but modifies html before finishing."
    (let ((file-path (org-html-publish-to-html plist filename pub-dir)))
      (message (format "blog-publish-html: fixing %s" filename))
      (with-temp-file file-path
        (insert-file-contents file-path)
        (goto-char (point-min))
        (search-forward "<body>")
        (insert "\n<div class=\"container hyphenate\">\n")
        (goto-char (point-max))
        (search-backward "</body>")
        (insert "\n</div>"))
      file-path))
#+end_src

#+RESULTS:
: blog-publish-html

Before we run this, we touch the file =index.org=, which is generated by the
sitemap function, see below. This triggers modification for that file so that
org will always publish the file. This is also from Narendra's blog.

#+begin_src emacs-lisp
(defun org-blog-prepare (project-plist)
  "With help from `https://github.com/howardabrams/dot-files'.
  Touch `index.org' to rebuilt it.
  Argument `PROJECT-PLIST' contains information about the current project."
  (progn
    (let* ((base-directory (plist-get project-plist :base-directory))
           (buffer (find-file-noselect (expand-file-name "index.org" base-directory) t)))
      (with-current-buffer buffer
        (set-buffer-modified-p t)
        (save-buffer 0))
      (kill-buffer buffer))))
#+end_src

** Generating the blog index
:PROPERTIES:
:ID:       6FF54B52-4825-4670-9849-1EBFA727ABE1
:END:

The blog index is generated using the sitemap function configured using
=:sitemap-function= and =:sitemap-format-entry=. The former receives all sitemap
entries each formatted by the latter.

The formating function formats an entry for the index list:

#+begin_src emacs-lisp
(defun org-blog-sitemap-format-entry (entry _style project)
  "Return string for each ENTRY in PROJECT."
  (when (s-starts-with-p "posts/" entry)
    (let ((subtitle (car (org-publish-find-property entry :subtitle project 'html)))
          (tags (org-publish-find-property entry :filetags project 'html)))
      (format "@@html:<h2 class=\"h4\">@@ [[file:%s][%s]] @@html:<small class=\"text-muted\">@@ %s @@html:</small></h2><p class=\"post-excerpt\">@@ %s @@html:</p>@@"
              entry
              (org-publish-find-title entry project)
              (format-time-string "%h %d, %Y"
                                  (org-publish-find-date entry project))
              subtitle
              (mapconcat (lambda (tag)
                           (format "@@html:<span class=\"badge badge-light\">@@ %s @@html:</span>@@" tag))
                         tags
                         "")))))
#+end_src

#+RESULTS:
: org-blog-sitemap-format-entry

And then the main function concatenates these posts into the index page.

#+begin_src emacs-lisp
(defun org-blog-sitemap-function (title list)
  "Return sitemap using TITLE and LIST returned by `org-blog-sitemap-format-entry'."
  (concat "#+TITLE: " title "\n"
          "#+OPTIONS: html-preamble:nil" "\n\n"
          (mapconcat (lambda (li)
                       (format "@@html:<article>@@ %s @@html:</article>@@" (car li)))
                     (seq-filter #'car (cdr list))
                     "\n")))
#+end_src

#+RESULTS:
: org-blog-sitemap-function

What actually the sitemap is about is that it generates an /org/ file that is
then also published by =org-publish=. 

** Post archive
:PROPERTIES:
:ID:       8E896CA3-9FEE-41C4-A4B2-FC9EF6FAEF6D
:END:

This turned out to be rather tricky! Since org does not have any sort of easy
support for generating pages with custom content, we need a way to generate a
post archive by year. Fortunately, you can have as many sitemaps as you want, so
all we have to do is create a second sitemap, called /archive/. This function
formats each entry and returns a hash table for each file containing the file
name, date and title, and generates a file called =archive.org= and then
the first project ("posts") grabs it and publishes it as the [[file:src/archive.org][Archive]].

#+begin_src emacs-lisp
  (defun blog-year-archive-publishing-function (_ _ _)
    "Does nothing, since we want the archive sitemap only to
    publish the sitemap, but not the posts"
    nil)

  (defun blog-year-archive-sitemap-entry (entry _style project)
    (when (s-starts-with-p "posts/" entry)
      (let ((date (org-publish-find-date entry project))
            (title (org-publish-find-title entry project)))
        `(:date ,date :entry ,entry :title ,title))))
#+end_src

#+RESULTS:
: blog-year-archive-sitemap-entry

Then, the /pièce de résistance/, the following function groups them by year
and splits them into different categories by year:

#+begin_src emacs-lisp
  (defun blog-year-archive-sitemap-function (title list)
    (let* ((posts (seq-remove #'null (-flatten-n 1 (cdr list))))
           (grouped (seq-group-by
                     (lambda (entry)
                       (when-let* ((date (plist-get entry :date))
                                   (year (nth 5 (decode-time date))))
                         year))
                     posts)))
      (mapconcat
       (lambda (grp)
         (let* ((year (car grp))
                (posts (cdr grp)))
           (concat
            "#+TITLE: " title "\n"
            "#+OPTIONS: html-preamble:nil" "\n\n"
            (format
             "@@html:<h4>@@ %s @@html:</h4>@@ \n" year)
            (mapconcat
             (lambda (post)
               (let ((title (plist-get post :title))
                     (date (plist-get post :date))
                     (file (plist-get post :entry)))
                 (format
                  "@@html:<div class=\"row\">@@ %s %s @@html:</div>@@"
                  (format
                   "@@html:<div class=\"col-sm-2\"><span class=\"text-muted\">@@ %s @@html:</span></div>@@"
                   (format-time-string "%d %B" date))
                  (format
                   "@@html:<div class=\"col-sm-10\">@@ [[file:%s][%s]] @@html:</div>@@"
                   file 
                   title))))
             posts
             "\n"))))
       grouped
       "\n")))

#+end_src

#+RESULTS:
: blog-year-archive-sitemap-function

** RSS

Getting RSS requires a third sitemap. How =ox-rss.el= works is that it parses a
file containing top-level headlines and generates a RSS =<item>= feed out of
them. I can't therefore use my =index.org= or =archive.org= to generate them as
they contain too much HTML markup and the titles are weirdly formatted
anyway. Fortunately, [[https://writepermission.com/org-blogging-rss-feed.html][Toon Claes]] had already found a solution for this, which is
just generating a third sitemap. This function publishes the main =feed.org=
file, and gives it a title and then formats =list= as headings.

#+begin_src emacs-lisp
  (defun blog-rss-sitemap (title list)
    "Create a RSS sitemap"
    (concat "#+TITLE: " title "\n\n"
            (org-list-to-subtree list '(:icount "" :istart ""))))
#+end_src

#+RESULTS:
: blog-rss-sitemap

Then we generate headings for all entries in the list. The two attributes
=RSS_PERMALINK= and =PUBDATE= need to be set by us, because if omitted, =ox-rss=
will try to invent suitable values for them. The publication date becomes the
current date and time of the publication invocation and that's not correct.
So by setting [[(pub)][PUBDATE]] we add the date from the =#+DATE= header to mark the publication date.

#+begin_src emacs-lisp -r 
  (defun blog-rss-sitemap-entry (entry style project)
    "Format ENTRY for the RSS feed.
  ENTRY is a file name.  STYLE is either 'list' or 'tree'.
  PROJECT is the current project."
    (cond ((not (directory-name-p entry))
           (let* ((file (org-publish--expand-file-name entry project))
                  (title (org-publish-find-title entry project))
                  (date (format-time-string "%Y-%m-%d" (org-publish-find-date entry project)))
                  (link (concat (file-name-sans-extension entry) ".html")))
             (with-temp-buffer
               (insert (format "* [[file:%s][%s]]\n" file title))
               (org-set-property "RSS_PERMALINK" link)
               (org-set-property "PUBDATE" date) (ref:pub)
               (org-id-get-create)
               (buffer-string))))
          ((eq style 'tree)
           ;; Return only last subdir.
           (file-name-nondirectory (directory-file-name entry)))
          (t entry)))
#+end_src

#+RESULTS:
: blog-rss-sitemap-entry

Lastly, we skip all files except =feed.org=, since we don't to only publish
=feed.org= as XML, not the posts.

#+begin_src emacs-lisp
  (defun blog-rss-publishing-function (plist filename pub-dir)
    "Publish RSS with PLIST, only when FILENAME is 'rss.org'.
  PUB-DIR is when the output will be placed."
    (if (equal "feed.org" (file-name-nondirectory filename))
        (org-rss-publish-to-rss plist filename pub-dir)))

#+end_src

#+RESULTS:
: blog-rss-publishing-function

** The configuration
:PROPERTIES:
:ID:       940FF517-F228-4280-A3D4-DB29275CB521
:END:

Now we've defined all we need for the project configuration. Without further
ado:

#+begin_src emacs-lisp 
  (setq org-publish-project-alist
        `(("posts"
           :base-directory ,(concat blog-root "src/")        ;; the sources of the posts
           :exclude ".*drafts/.*"                            ;; don't publish draft posts
           :base-extension "org"                             ;; only org files

           :publishing-directory ,(concat blog-root "out/")  ;; results go here
           :publishing-function blog-publish-html            ;; the function we use to publish

           :preparation-function org-blog-prepare            ;; called before publishing
           :recursive t                                      ;; publish all files recursively

           :html-link-home "/"                               ;; home points here
           :html-link-up "/"                                 ;; up as well
           :html-head-extra ,blog-html-head-extra            ;; <head> conents
           :html-head-include-scripts t                      ;; add org built-in js 
           :html-head-include-default-style nil              ;; no org default css
           :html-home/up-format ,blog-html-up                ;; the navigation menu
           :html-preamble blog-html-preamble-fmt             ;; title (preamble) formating
           :html-postamble ,blog-html-down                   ;; footer
           :html-metadata-timestamp-format "%e %B %Y"        ;; timestamps in the footer

           :with-toc nil                                     ;; no toc by default
           :with-title nil                                   ;; no title either
           :with-date t                                      ;; has no effect either
           :section-numbers nil                              ;; no section numbers
           :html-doctype "html5"                             ;; fancy semantic html5 tags
           :html-html5-fancy t                               ;; fancy the max
           :htmlized-source t                                ;; use CSS for sources, instead of inline 

           :auto-sitemap t                                   ;; publish the index
           :sitemap-filename "index.org"                     ;; to this file
           :sitemap-title "Index"                            ;; with this title
           :sitemap-style list                               ;; no effect

           ;; these functions determine the style of the index
           :sitemap-sort-files anti-chronologically
           :sitemap-format-entry org-blog-sitemap-format-entry
           :sitemap-function org-blog-sitemap-function
           ;; )))
           )
          ("assets"
           :base-directory ,(concat blog-root "src/assets/")
           :base-extension "png\\|css\\|svg\\|js"
           :publishing-directory ,(concat blog-root "out/assets/")
           :publishing-function org-publish-attachment
           :recursive t)
          ("archive"
           :base-directory ,(concat blog-root "src/")
           :base-extension "org"
           :recursive t

           :publishing-function blog-year-archive-publishing-function
           :publishing-directory ,(concat blog-root "out/") ;
           :auto-sitemap t
           :sitemap-filename "archive.org"
           :sitemap-title "Archive"
           :sitemap-style list
           :sitemap-function blog-year-archive-sitemap-function
           :sitemap-format-entry blog-year-archive-sitemap-entry

           :sitemap-sort-files anti-chronologically
           )
          ("rss"
           :base-directory ,(concat blog-root "src/")
           :base-extension "org"
           :exclude ,(regexp-opt '("archive.org" "index.org" "about.org"))
           :recursive t

           :description "The website of Antoine Kalmbach"
           :html-link-home "http://ane.github.io/"
           :html-link-use-abs-url t
           :html-link-org-files-as-html t
           :rss-extension "xml"

           :publishing-directory ,(concat blog-root "out/") ;
           :publishing-function blog-rss-publishing-function

           :auto-sitemap t
           :sitemap-filename "feed.org"
           :sitemap-title "Feed"
           :sitemap-style list
           :sitemap-function blog-rss-sitemap
           :sitemap-format-entry blog-rss-sitemap-entry
           :sitemap-sort-files anti-chronologically

           :with-toc nil
           :section-numbers nil)
          ("blog"
           :components ("assets" "archive" "rss" "posts"))))

#+end_src

#+RESULTS:
| posts | :base-directory | /Users/akalmbach/code/kakka/src/ | :exclude | .*drafts/.* | :base-extension | org | :publishing-directory | /Users/akalmbach/code/kakka/out/ | :publishing-function | blog-publish-html | :preparation-function | org-blog-prepare | :recursive | t | :html-link-home | / | :html-link-up | / | :html-head-extra | <link rel="stylesheet" href="/assets/style.css" type="text/css"> |

This creates three components for the =blog= project:

- **posts**, the blog posts used to generate the [[file:src/index.org][index]]
- **archive**, which is a secondary sitemap used to generate the [[file:src/archive.org][archive]]
- **assets**, a project that just copies the images and other static crap to the
  output directory
- **rss** that publishes RSS

So, now hitting either @@html:<kbd>F3</kbd>@@ will publish the =blog= project,
which publishes everything I need. Next, it's time for something completely different.
